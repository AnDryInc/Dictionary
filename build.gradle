buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}
configurations {
    smallicu
}
dependencies {
    compile 'com.android.support:appcompat-v7:22.2.1'
    // Note: you can just replace "smallicu" by compile
    // and remove the "compile files" part if you do not
    // care about the size of the apk
    smallicu 'com.ibm.icu:icu4j:55.1'
    compile files('build/icu4j-55.1.small.jar') {
        builtBy 'makesmallicu'
    }
}

task makesmallicu (dependsOn: configurations.smallicu) << {
    exec {
        commandLine "./mksmallicu.sh", "${configurations.smallicu.resolve()[0]}", "build/icu4j-55.1.small.jar"
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.1"
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', '../Util/src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        applicationId 'de.reimardoeffinger.quickdic'
        targetSdkVersion 22
    }
}
